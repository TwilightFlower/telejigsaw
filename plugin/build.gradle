plugins {
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = "io.github.twilightflower.telejigsaw"
archivesBaseName = "telejigsaw"
version = "0.1.0"

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		name = "HalfOf2"
		url = uri("https://storage.googleapis.com/devan-maven/")
	}
	maven { url 'https://maven.fabricmc.net/' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
	maven { url 'https://maven.hydos.cf/releases/' }
}

configurations {
	shade
}

dependencies {
	shade implementation("io.github.astrarre.amalgamation:gradle-plugin:2.3.0") {
		exclude group: "net.minecraftforge"
	}
	
	shade implementation("org.quiltmc:quiltflower:1.5.0")
}

shadowJar {
	configurations = [project.configurations.shade]
}

gradlePlugin {
	plugins {
		telejigsaw {
			id = 'telejigsaw'
			implementationClass = 'io.github.twilightflower.telejigsaw.TeleJigsawPlugin'
		}
		automatedPublishing = false
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(16)
	}
	
	withSourcesJar()
}

publishing {
	repositories {
		mavenLocal()
	}
	
	publications {
		pluginHack(MavenPublication) {
			groupId = "telejigsaw"
			artifactId = "telejigsaw.gradle.plugin"
			
			artifact(shadowJar) {
				classifier = ''
				builtBy shadowJar
			}
		}
		
		maven(MavenPublication) {
			groupId = "io.github.twilightflower"
			artifactId = "telejigsaw"
			
			from components.java
		}
	}
}
